% Single fuel cell

set title "Single+fuel+cell"

% Set seed to get same results

set seed 1474468046

% Moderator region:

surf 1 sqc 0.0 0.0 0.6475

% Fuel region:

surf 2 cyl 0.0 0.0 4.33500E-01

% Cell definitions:

cell  1 0 fuel2		-2	%Simple fuel pin, no cladding or gap
cell  2 0 cool 	 	 2 -1	%Coolant in cell
cell 99 0 outside 	 1	%Outside world

% Fuel materials:

mat fuel1   -10.424 rgb 0 255 0
 92235.09c   -0.015867
 92238.09c   -0.86563
  8016.09c   -0.1185

mat fuel2   -10.424
 92235.09c   -0.018512
 92238.09c   -0.86299
  8016.09c   -0.1185

mat fuel3   -10.424
 92235.09c   -0.022919
 92238.09c   -0.85858
  8016.09c   -0.1185

mat fuel4   -10.424
 92235.09c   -0.026445
 92238.09c   -0.85505
  8016.09c   -0.1185

mat fuel5   -10.424
 92235.09c   -0.029971
 92238.09c   -0.85153
  8016.09c   -0.1185

mat fuel6   -10.424
 92235.09c   -0.032615
 92238.09c   -0.84888
  8016.09c   -0.1185

% Coolant (40% void fraction):

mat cool     -0.443760  moder lwtr 1001 rgb 0 255 255
 1001.06c     0.66667
 8016.06c     0.33333

% Thermal scattering data for light water:

therm lwtr lwj3.11t

% Cross section data library file path:

set acelib "/home/segonpin/codesother/Serpent/xsdata/jeff311/sss_jeff311u.xsdata"


% Reflective boundary condition:

set bc 2   %changed from reflective to periodic boundary condition

% Neutron population and criticality cycles:

set pop 2000 500 50

% Geometry and mesh plots:

plot 3 500 500
%mesh 3 500 500

% Set energy grids
ene 2gthm	1 0		0.625E-6
ene 2gfast	1 0.625E-6 	20

% Boundary surfaces for detectors:
surf W px -0.6475			%West
surf E px  0.6475			%East
surf S py -0.6475			%South
surf N py  0.6475			%North




% %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% fast and thermal group %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%  FUEL TOTAL  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

det rfueltotfast dc 1 de 2gfast dr -1 fuel2  % USEFUL
% ------------------------------------------------------------------------------------------
det rfueltotthm dc 1 de  2gthm dr -1 fuel2 

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%  FUEL SURFACE DETECTORS %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%  IN AND OUT   %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% ----------------------------------------------------------------------------------------------------
% --- Detector for outward current on fuel boundary, exclude those neutrons coming from outside the fuel 
% --- region and crossing the region without interacting
det jinfuelfast ds 2 -1 de 2gfast dfl 40 1
det joutfuelfast ds 2 1 de 2gfast dfl 40 3 dfl 41 1
% ------------------------------------------------------------------------------------------
det jinfuelthm ds 2 -1 de 2gthm dfl 50 1
det joutfuelthm ds 2 1 de 2gthm dfl 50 3 dfl 51 1


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%  SURF AND MOD TO FUEL %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

det rmjinfuelfast dc 1 dr -1 fuel2 de 2gfast dfl 40 2 dfl 40 0
% ------------------------------------------------------------------------------------------
det rmjinfuelthm dc 1 dr -1 fuel2  de 2gthm  dfl 50 2 dfl 50 0


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%  FUEL TO MOD  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% -- Detector for number of neutrons emitted from fuel region to first interact in moderator

det rfueltomodfast dc 2 dr -1 cool de 2gfast dfl 41 2 dfl 41 0
% ------------------------------------------------------------------------------------------
det rfueltomodthm dc 2  dr -1 cool de 2gthm  dfl 51 2 dfl 51 0


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%  FUEL TO SURF  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% --- Detector for neutron emitted from fuel to left the surfaces
det rfueltowfast ds W -1 de 2gfast dfl 41 2 dfl 41 0
det rfueltoefast ds E  1 de 2gfast dfl 41 2 dfl 41 0
det rfueltosfast ds S -1 de 2gfast dfl 41 2 dfl 41 0
det rfueltonfast ds N  1 de 2gfast dfl 41 2 dfl 41 0
% ------------------------------------------------------------------------------------------

det rfueltowthm ds W -1 de 2gthm dfl 51 2 dfl 51 0
det rfueltoethm ds E  1 de 2gthm dfl 51 2 dfl 51 0
det rfueltosthm ds S -1 de 2gthm dfl 51 2 dfl 51 0
det rfueltonthm ds N  1 de 2gthm dfl 51 2 dfl 51 0

